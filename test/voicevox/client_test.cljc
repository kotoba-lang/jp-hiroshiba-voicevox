(ns voicevox.client-test
  (:require [clojure.test :refer [deftest is testing]]
            [voicevox.client :as client]))

(deftest audio-query-test
  (testing "happy path"
    (let [calls (atom [])
          stub (fn [req] (swap! calls conj req)
                 {:status 200 :body-bytes (.getBytes "{\"speedScale\":1.0,\"pitchScale\":0.0}" "UTF-8")})
          result (client/audio-query! 2 "こんにちは" {:http-fn stub :base-url "http://x"})]
      (is (= 1.0 (:speedScale result)))
      (is (.contains (:url (first @calls)) "speaker=2"))
      (is (.contains (:url (first @calls)) "text="))
      (is (= :post (:method (first @calls))))))
  (testing "failure raises"
    (let [stub (fn [_] {:status 500 :body-bytes (.getBytes "err" "UTF-8")})]
      (is (thrown? clojure.lang.ExceptionInfo
                   (client/audio-query! 2 "hi" {:http-fn stub :base-url "http://x"}))))))

(deftest synthesis-test
  (testing "happy path returns raw bytes"
    (let [calls (atom [])
          stub (fn [req] (swap! calls conj req) {:status 200 :body-bytes (byte-array [82 73 70 70])})
          wav (client/synthesis! 2 {:speedScale 1.0} {:http-fn stub :base-url "http://x"})]
      (is (= [82 73 70 70] (vec wav)))
      (is (.contains (:url (first @calls)) "speaker=2"))
      (is (= "audio/wav" (get (:headers (first @calls)) "Accept")))))
  (testing "failure raises"
    (let [stub (fn [_] {:status 400 :body-bytes (.getBytes "bad" "UTF-8")})]
      (is (thrown? clojure.lang.ExceptionInfo
                   (client/synthesis! 2 {} {:http-fn stub :base-url "http://x"}))))))

(deftest version-test
  (let [stub (fn [_] {:status 200 :body-bytes (.getBytes "0.21.0" "UTF-8")})]
    (is (= "0.21.0" (client/version! {:http-fn stub :base-url "http://x"})))))
